** Pulse Modulation

.lib LTspiceControlLibrary\Sources.lib

.subckt 3PhaseComplementaryPWM U V W up un vp vn wp wn
X1 CN 0 TriangleWaveVoltage V1={Vin1} V2={Vin2-Vdead} Tperiod={Tperiod} tratio=0.5 Tdelay={Tdelay} Ncycles=0
B1 CP 0 V=V(CN)+Vdead
B2 up 0 V=V(U)>=V(CP) tripdt={tripdt}
B3 un 0 V=V(U)<=V(CN) tripdt={tripdt}
B4 vp 0 V=V(V)>=V(CP) tripdt={tripdt}
B5 vn 0 V=V(V)<=V(CN) tripdt={tripdt}
B6 wp 0 V=V(W)>=V(CP) tripdt={tripdt}
B7 wn 0 V=V(W)<=V(CN) tripdt={tripdt}
.params Vin1=-1 Vin2=1 Tperiod=100u Tdead=1u Tdelay=0 tripdt=1*Tdead
.params Vdead=2*(Vin2-Vin1)/(Tperiod/Tdead+2)
.ends 3PhaseComplementaryPWM
