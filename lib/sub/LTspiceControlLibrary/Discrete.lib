** Discrete

.subckt ZeroOrderHold Y U
V1 CLK 0 PULSE(0 1 0 {Tr} {Tr} 0 {T})
A1 U 0 CLK 0 0 0 D0 0 SAMPLEHOLD Vlow={Ymin} Vhigh={Ymax}
B1 Y 0 V=if(time>Tr,V(D0),V(U)) tripdt={tripdt} tripdv={tripdv}
.param T=1 Tr=1m*T
.param Ymin=-10k Ymax=10k
.param tripdt=1e300 tripdv=1
.ends ZeroOrderHold

.subckt UnitDelay Y U
V1 NCLK 0 PULSE(1 0 0 {Tr} {Tr} 0 {T})
V2 CLK 0 PULSE(0 1 0 {Tr} {Tr} 0 {T})
A1 U 0 NCLK 0 0 0 D0 0 SAMPLEHOLD Vlow={Ymin} Vhigh={Ymax}
A2 D0 0 CLK 0 0 0 D1 0 SAMPLEHOLD Vlow={Ymin} Vhigh={Ymax}
B1 Y 0 V=if(time>(T+Tr),V(D1),Y0) tripdt={tripdt} tripdv={tripdv}
.param T=1 Tr=1m*T
.param Y0=0 Ymin=-10k Ymax=10k
.param tripdt=1e300 tripdv=1
.ends UnitDelay

.subckt Previous Y U
.machine
.output (pre_U) V(U)
.endmachine
R1 pre_U 0 1k
B1 0 pre_U I=if(time>0,0,1m*V(U)) tripdt=1e300
E1 Y 0 pre_U 0 1
.ends
