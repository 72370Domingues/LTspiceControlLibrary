** Transfer Functions

.subckt Proportional Y U
B1 Y 0 V=K*V(U)
.params K=1
.ends Proportional

.subckt Derivative Y U
B1 Y 0 V=ddt(V(U))*T
.params T=1
.ends Derivative

.subckt Integral Y U
B1 Y 0 V=idt(V(U),Y0*T)/T
.params T=1 Y0=0
.ends Integral

.subckt LimitedIntegral Y U
B1 Y 0 V=idt(V(U),if(V(Y)>=Ymax,Ymax,if(V(Y)<=Ymin,Ymin,Y0))*T,((V(Y)>=Ymax)&(V(U)>0))|((V(Y)<=Ymin)&(V(U)<0)))/T
.params T=1 Y0=0 Ymin=-1T Ymax=1T
.ends LimitedIntegral

.subckt 1stOrderLag Y U
E1 Y 0 U 0 Laplace = {K/(1+T*s)}
.params K=1 T=1
.ends 1stOrderLag

.subckt 2ndOrderLag Y U
E1 Y 0 U 0 Laplace = {(K*w*w)/(s*s+2*z*w*s+w*w)}
.params K=1 w=1 z=1
.ends 2ndOrderLag

.subckt DeadTime Y U
B1 Y 0 V=delay(V(U),T)
.params T=1
.ends DeadTime

.subckt LaggedDerivative Y U
E1 Y 0 U 0 Laplace = {(T*s)/(1+i*T*s)}
.params i=1 T=1
.ends LaggedDerivative
