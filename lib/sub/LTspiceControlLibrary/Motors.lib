** Motors

.subckt DCMotor e+ e- m+ m-
Re1 e+ Ne01 {Ra}
Le1 Ne01 Ne02 {La} Rser=0
Be1 Ne02 e- V=-Ke*I(Bm1)
Rm1 m+ Nm01 {Dm}
Lm1 Nm01 Nm02 {Jm} Rser=0
Bm1 Nm02 m- V=Kt*I(Be1)
.params Ra=1 La=1m Ke=0.01 Dm=10u Jm=10u
.params Kt=Ke
.ends DCMotor

.subckt PMSMotor U V W m+ m-
Rd1 Vd Nd01 {Ra}
Ld1 Nd01 Nd02 {Ld} Rser=0
Bd1 Nd02 0 V=-Lq*I(Bq1)*V(Oe)
Rq1 Vq Nq01 {Ra}
Lq1 Nq01 Nq02 {Lq} Rser=0
Bq1 Nq02 Nq03 V=Ld*I(Bd1)*V(Oe)
Bq2 Nq03 0 V=Ke*V(Oe)
Rm1 m+ Nm01 {Dm}
Lm1 Nm01 Nm02 {Jm} Rser=0
Bm1 Nm02 Nm03 V=Np*(Ld-Lq)*I(Bd1)*I(Bq1)
Bm2 Nm03 m- V=Np*Kt*I(Bq1)
Bu1 U 0 I=sqrt(2/3)*V(Ia)
Bv1 V 0 I=-I(Bu1)-I(Bw1)
Bw1 W 0 I=sqrt(1/6)*(-V(Ia)-sqrt(3)*V(Ib))
B1 Om 0 V=-I(Bm1)
B2 Pm 0 V=idt(V(Om),Pm0)
B3 Oe 0 V=Np*V(Om)
B4 Pe 0 V=Np*V(Pm)
B5 sinPe 0 V=sin(V(Pe))
B6 cosPe 0 V=cos(V(Pe))
B7 Va 0 V=sqrt(1/6)*(V(U,V)+V(U,W))
B8 Vb 0 V=sqrt(1/2)*V(V,W)
B9 Vd 0 V=V(Va)*V(cosPe)+V(Vb)*V(sinPe)
B10 Vq 0 V=-V(Va)*V(sinPe)+V(Vb)*V(cosPe)
B11 Ia 0 V=I(Bd1)*V(cosPe)-I(Bq1)*V(sinPe)
B12 Ib 0 V=I(Bd1)*V(sinPe)+I(Bq1)*V(cosPe)
.params Ra=0.1 Ld=1m Lq=1m Ke=0.1 Dm=0.1m Jm=0.1m Np=3
.params Kt=Ke
.params Pm0=0
.ends PMSMotor

.subckt InductionMotor U V W m+ m- fd2 fq2
Rd11 Vd1 Nd101 {R1}
Ld11 Nd101 Nd102 {Ll1} Rser=0
Ld12 Nd102 0 {M} Rser=0
Rq11 Vq1 Nq101 {R1}
Lq11 Nq101 Nq102 {Ll1} Rser=0
Lq12 Nq102 0 {M} Rser=0
Rd21 0 Nd201 {R2}
Ld21 Nd201 Nd202 {Ll2} Rser=0
Ld22 Nd202 Nd203 {M} Rser=0
Bd21 Nd203 0 V=(M*I(Lq11)+L2*I(Lq21))*V(Oe2)
Rq21 0 Nq201 {R2}
Lq21 Nq201 Nq202 {Ll2} Rser=0
Lq22 Nq202 Nq203 {M} Rser=0
Bq21 Nq203 0 V=-(M*I(Ld11)+L2*I(Ld21))*V(Oe2)
Rm1 m+ Nm01 {Dm}
Lm1 Nm01 Nm02 {Jm} Rser=0
Bm1 Nm02 m- V=Np*M*(I(Lq11)*I(Ld21)-I(Ld11)*I(Lq21))
Kd1 Ld12 Ld22 1
Kq1 Lq12 Lq22 1
Bu1 U 0 I=sqrt(2/3)*I(Ld11)
Bv1 V 0 I=-I(Bu1)-I(Bw1)
Bw1 W 0 I=sqrt(1/6)*(-I(Ld11)-sqrt(3)*I(Lq11))
B1 Oe2 0 V=-Np*I(Bm1)
B2 Vd1 0 V=sqrt(1/6)*(V(U,V)+V(U,W))
B3 Vq1 0 V=sqrt(1/2)*V(V,W)
B4 fd2 0 V=M*I(Ld11)+L2*I(Ld21)
B5 fq2 0 V=M*I(Lq11)+L2*I(Lq21)
.params R1=1 R2=1 M=0.1 Ll1=1m Ll2=1m Dm=10m Jm=10m Np=2
.params L1=M+Ll1 L2=M+Ll2
.ends InductionMotor
